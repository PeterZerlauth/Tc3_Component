<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_ListElement" Id="{38419e87-5698-4e05-9e30-1094a3671d1c}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'no_explicit_call' := 'do not call this POU directly'} 
FUNCTION_BLOCK FB_ListElement IMPLEMENTS I_ListElement
VAR
	pList: 			POINTER TO I_Element;
	{IF (defined (release))}
    	{attribute 'hide'}
	{END_IF}
	aList:			POINTER TO POINTER TO ARRAY [0..24] OF I_Element:=ADR(pList);
END_VAR
VAR_OUTPUT
	{IF (defined (release))}
    	{attribute 'hide'}
	{END_IF}
	nLength:		INT;	
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// https://peterzerlauth.com/]]></ST>
    </Implementation>
    <Method Name="FB_exit" Id="{86b1742d-19e1-4a3d-942a-39b46efa4e7f}">
      <Declaration><![CDATA[METHOD FB_exit : BOOL
VAR_INPUT
	bInCopyCode : BOOL; // if TRUE, the exit method is called for exiting an instance that is copied afterwards (online change).
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[M_Clear();]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Add" Id="{2b676e03-0a2e-4542-9d45-8d0e1be34635}">
      <Declaration><![CDATA[METHOD PUBLIC M_Add : BOOL
VAR_INPUT
	iObject: 	I_Element;
END_VAR
VAR
	pOldList: 	POINTER TO I_Element;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF iObject <> 0 THEN
	// First Item
	IF pList = 0 THEN
		nLength:= nLength + 1;
		pList:= __NEW(POINTER TO I_Element,DINT_TO_UDINT(nLength));
	ELSE
		// Item already in List
		IF M_Find(iObject) = -1 THEN
			// backup 
			pOldList:= pList;
			// new Length
			nLength:= nLength + 1;
			// new pointer
			pList:= __NEW(POINTER TO I_Element,DINT_TO_UDINT(nLength));
			// restore
			Memcpy(pList,pOldList, SIZEOF(POINTER TO I_Element)*DINT_TO_UDINT(nLength -1));
			// delete old
			__DELETE(pOldList);
		ELSE
			M_Add:= FALSE;	
			RETURN;
		END_IF
	END_IF
	IF pList = 0 THEN
		RETURN;
	END_IF
	// add new Object
	pList[nLength-1]:= iObject;
	M_Add:= TRUE;
ELSE
	M_Add:= FALSE;	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Clear" Id="{3e2c5687-bf15-40f5-99d0-ac9877db5017}">
      <Declaration><![CDATA[METHOD PUBLIC M_Clear : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Clear
IF pList <> 0 THEN
	nLength:= 0;
	__DELETE(pList);
END_IF
M_Clear:= TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Find" Id="{20062c4e-124c-4cb3-98f6-8054f6c60d5b}">
      <Declaration><![CDATA[METHOD PUBLIC M_Find : DINT
VAR_INPUT
	iObject: 	I_Element;
END_VAR
VAR
	nIndex: 	UINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Find
M_Find := -1;
WHILE nIndex < nLength DO
    IF (pList[nIndex] = iObject) THEN
        M_Find := nIndex;
        RETURN;
    END_IF
	nIndex := nIndex + 1;
END_WHILE
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Index" Id="{9eaa9448-53ef-4c51-8a5f-0c3baada6113}">
      <Declaration><![CDATA[METHOD PUBLIC M_Index : I_Element
VAR_INPUT
	Index: 	DINT;
END_VAR
VAR
	pTest: 			I_Element:= pList[Index];
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Index
IF Index <= nLength THEN
	M_Index:= pList[Index];
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Remove" Id="{e210f5d7-9bdf-4aee-9085-7160fb8ea729}">
      <Declaration><![CDATA[METHOD PUBLIC M_Remove : BOOL
VAR_INPUT
	iObject: 	I_Element;
END_VAR
VAR
	pOldList:			POINTER TO I_Element;
	nPosition: 			DINT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Remove
IF iObject <> 0 THEN
	// First Item
	IF nLength >= 0 THEN
		// Item already in List
		nPosition:= M_Find(iObject);
		IF nPosition <> -1 THEN
			// backup 
			pOldList:= pList;
			// new Length
			nLength:= nLength -1;
			// new pointer
			pList:= __NEW(POINTER TO I_Element,DINT_TO_UDINT(nLength));
			// restore lower part
			Memcpy(pList,pOldList, SIZEOF(POINTER TO I_Object) * DINT_TO_UDINT(nPosition));
			//pList[nPosition]:= iObject;
			Memcpy(pList + (SIZEOF(POINTER TO I_Element)*nPosition),pOldList + (SIZEOF(POINTER TO I_Element)*(nPosition + 1)), SIZEOF(POINTER TO I_Element) * DINT_TO_UDINT(nLength - nPosition));
			// delete old
			__DELETE(pOldList);
		ELSE
			M_Remove:= FALSE;	
			RETURN;
		END_IF
	ELSE
		M_Remove:= FALSE;	
		RETURN;
	END_IF
	M_Remove:= TRUE;
ELSE
	M_Remove:= FALSE;	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_Length" Id="{a7fee46d-1216-4c1b-b269-41e22da51578}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC P_Length : DINT
]]></Declaration>
      <Get Name="Get" Id="{a55579c9-f979-411c-8831-bafff0ca169f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Length:= nLength;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>