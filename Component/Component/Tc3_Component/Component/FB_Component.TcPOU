<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Component" Id="{3beb4649-c557-0131-2fab-e053b34b5bbf}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'reflection'} 
{attribute 'no_explicit_call' := 'do not call this POU directly'} 
FUNCTION_BLOCK FB_Component IMPLEMENTS I_Component, I_Notify
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
    {attribute 'hide'}
	{attribute 'instance-path'} 
    {attribute 'noinit'}
    sInstancePath:					STRING(255);				// Instance-Path
    {attribute 'hide'}
	fbInfo:							FB_Info(sInstancePath);		// Info about function block
	fbEvent:						FB_Event(THIS^);			// Event creator
	fbSequence:						FB_Sequence(THIS^);			// Sequence (abort = reset)
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	eStateRequest:					E_Request;					// State command
    {attribute 'hide'}
	eState:							E_State;					// Current state
    {attribute 'hide'}
	eStatePrevious:					E_State;					// Previous state
	{attribute 'OPC.UA.DA' := '1'}
	{attribute 'OPC.UA.DA.Access' := '1'}
	eModeRequest:					E_Mode;						// Requested mode
    {attribute 'hide'}
	eMode:							E_Mode;						// Current mode
    {attribute 'hide'}
	eModePrevious:					E_Mode;						// Previous mode	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// https://github.com/PeterZerlauth/Tc3_Component]]></ST>
    </Implementation>
    <Folder Name="Command" Id="{9635140d-5b18-006e-1e9e-963cbb8377f9}" />
    <Folder Name="State" Id="{ca15ba5f-c875-08b1-19c6-3566ddf8fed6}" />
    <Method Name="M_Holding" Id="{0453e1ef-1254-097d-2836-c180f9c76437}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Holding : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Holding
M_Holding:= True;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Execute" Id="{0a26e879-26ba-0448-077b-265f6eb411a3}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Execute : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[M_Execute:= True;]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_Info" Id="{1f2686a8-22c2-03c1-3b18-3cdd0f1b6158}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC P_Info : I_Info]]></Declaration>
      <Get Name="Get" Id="{80d9a233-1863-0529-037a-9eeb24a670c8}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Info:= fbInfo;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="M_Initialise" Id="{25962ce0-1492-0d73-2226-fd61bbb69809}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Initialise
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Paused" Id="{2c298a87-8477-08fb-1e4e-6f14369f8f00}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Paused // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Unholding" Id="{38e86df5-306b-0975-32bb-19e03b5147dc}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Unholding : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Holding
M_Unholding:= True;]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_Mode" Id="{3bb67a0e-6846-07bc-053e-b55a797fa512}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC P_Mode : E_Mode]]></Declaration>
      <Get Name="Get" Id="{9d5d15ee-5102-0655-3457-a8f0100c4631}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Mode:= eMode;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="P_ID" Id="{3effb9b7-1c02-072b-311e-de8397111421}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC P_ID : STRING(25)]]></Declaration>
      <Get Name="Get" Id="{0c21d00d-b7a9-0d38-0c40-7f63c6fb1b7c}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_ID:= fbInfo.sIdentifier;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{9f14bced-b370-082e-1cc1-0212a2a60675}">
        <Declaration><![CDATA[PUBLIC 
VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[fbInfo.sIdentifier:= P_ID;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Method Name="M_Stopped" Id="{415168e7-f2ee-0214-0037-987f9ecb70ed}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Stopped
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Completed" Id="{47ba5a0a-8800-0f93-2645-874f5aa969a5}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Completed
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_OnState" Id="{49afef72-99ac-0c9e-2ec3-7701e0509cd3}">
      <Declaration><![CDATA[METHOD Protected M_OnState
VAR_INPUT
	eState:							E_State;						// Current state
	eStatePrevious:					E_State;						// Previous state	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_Event" Id="{4af7c41c-ce75-074c-247b-1e9cde87d3d0}">
      <Declaration><![CDATA[PROPERTY PUBLIC P_Event : I_Event
]]></Declaration>
      <Get Name="Get" Id="{581dbbf1-1871-0bc4-2167-11bb575e35b8}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Event:= fbEvent;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="P_Sequence" Id="{4fcbc993-ca53-00ae-2678-b4843b89ffee}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC P_Sequence : I_Sequence]]></Declaration>
      <Get Name="Get" Id="{8dc7e634-55d3-030d-2d39-2f9110866178}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Sequence:= fbSequence;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="M_OnMode" Id="{5404d353-f502-0cc7-293b-81200dd1aa4b}">
      <Declaration><![CDATA[METHOD PROTECTED M_OnMode
VAR_INPUT
	eMode:							E_Mode;						// Current mode
	eModePrevious:					E_Mode;						// Previous mode	
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Aborting" Id="{5420cd8c-8440-0133-17b0-cfc9fa3a226e}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Aborting : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Aborting
M_Aborting:= TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Stopping" Id="{752f6f5c-84eb-0065-086e-87701d5b75a9}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Stopping : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Stopping
M_Stopping:= True;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_HasMode" Id="{75908801-23b6-0dfd-2c6e-534bb3174ba2}">
      <Declaration><![CDATA[METHOD PROTECTED M_HasMode : BOOL
VAR_INPUT
	eMode:			E_Mode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[M_HasMode:= THIS^.eMode = eMode;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Starting" Id="{7b4cc2a9-4be8-0ad5-3afd-3314d7249ec2}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Starting : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Starting
M_Starting:= True;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Resuming" Id="{811e0c52-9853-075c-3d1b-5e25fdee4124}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Resuming : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Holding
M_Resuming:= True;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Cycle" Id="{8157620c-1844-0b4a-092c-fe72147fa392}">
      <Declaration><![CDATA[METHOD PUBLIC M_Cycle
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[fbInfo();
fbEvent();
fbSequence();

// Handle Module Type Package states
CASE eState OF
	E_State.Undefined:
		M_Initialise();
		eMode:= E_Mode.Automatic;
		eState:= E_State.Idle;
				
	E_State.Resetting:
		M_Running();
		IF M_Resetting() THEN
			eState:= E_State.Idle;
			eStateRequest:= E_Request.Undefined;
		END_IF
		CASE eStateRequest OF
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;
				
		END_CASE
		
	E_State.Idle:
		M_Running();
		M_Idle();
		CASE eStateRequest OF
			
			E_Request.Start:
				eState:= E_State.Starting;
				
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;
				
			ELSE
				eStateRequest:= E_Request.Undefined;
				
		END_CASE

	E_State.Starting:
		M_Running();
		IF M_Starting() THEN
			eState:= E_State.Execute;
			eStateRequest:= E_Request.Undefined;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;
				
		END_CASE

	E_State.Execute:
		M_Running();
		IF M_Execute() THEN
			CASE eStateRequest OF
				
				E_Request.Complete:
					eState:= E_State.Completing;
					
			END_CASE
		END_IF
		CASE eStateRequest OF
			E_Request.Complete:;
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;

			E_Request.Hold:
				eState:= E_State.Holding;
				
			E_Request.Pause:
				eState:= E_State.Pausing;
				
			ELSE
				eStateRequest:= E_Request.Undefined;
				
		END_CASE
		
	E_State.Stopping:
		M_Running();
		IF M_Stopping() THEN
			eState:= E_State.Stopped;
			eStateRequest:= E_Request.Undefined;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Abort:
				eState:= E_State.Aborting;
				
		END_CASE

	E_State.Stopped:
		M_Stopped();
		CASE eStateRequest OF
			
			E_Request.Abort:
				eState:= E_State.Aborting;
				
			E_Request.Reset:
				eState:= E_State.Resetting;
				
		ELSE
			eStateRequest:= E_Request.Undefined;				
		END_CASE
		
		
	E_State.Aborting:
		fbSequence.M_Reset();
		IF M_Aborting() THEN
			eState:= E_State.Aborted;
			eStateRequest:= E_Request.Undefined;
		END_IF
		
	E_State.Aborted:
		M_Aborted();
		CASE eStateRequest OF
			
			E_Request.Reset:
				eState:= E_State.Resetting;
				
		ELSE
			eStateRequest:= E_Request.Undefined;				
		END_CASE

		
	E_State.Pausing:
		M_Running();
		IF M_Pausing() THEN
			eState:= E_State.Paused;
			eStateRequest:= E_Request.Undefined;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;

		END_CASE
		
	
	E_State.Paused:
		M_Running();
		M_Paused();
		CASE eStateRequest OF
			
			E_Request.Start:
				eState:= E_State.Resuming;
				
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;
						
		ELSE
			eStateRequest:= E_Request.Undefined;

		END_CASE
		
	E_State.Resuming:
		M_Running();
		IF M_Resuming() THEN
			eState:= E_State.Execute;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;

		END_CASE

	E_State.Holding:
		M_Running();
		IF M_Holding() THEN
			eState:= E_State.Held;
			eStateRequest:= E_Request.Undefined;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;

		END_CASE

	E_State.Held:
		M_Running();
		M_Held();
		CASE eStateRequest OF
			
			E_Request.Start:
				eState:= E_State.Unholding;

			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;
				
			ELSE
				eStateRequest:= E_Request.Undefined;

		END_CASE
		
	E_State.Unholding:
		M_Running();
		IF M_Unholding() THEN
			eState:= E_State.Execute;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;

		END_CASE
		
	E_State.Completing:
		M_Running();
		IF M_Completing() THEN
			eState:= E_State.Completed;
			eStateRequest:= E_Request.Undefined;
		END_IF
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;

		END_CASE
		
	E_State.Completed:
		M_Running();
		M_Completed();
		CASE eStateRequest OF
			
			E_Request.Stop:
				eState:= E_State.Stopping;
				
			E_Request.Abort:
				eState:= E_State.Aborting;
					
			E_Request.Reset:
				eState:= E_State.Resetting;
				
			ELSE
				eStateRequest:= E_Request.Undefined;
				
		END_CASE
		
END_CASE

IF eModeRequest <> E_Mode.Undefined THEN
	IF eMode <> eModeRequest THEN
		eModePrevious:= eMode;
		eMode:= eModeRequest;
		M_OnMode(eMode, eModePrevious);
	END_IF
END_IF
eModeRequest:= E_Mode.Undefined;
	
// Detect state changed
IF eStatePrevious <> eState THEN
	M_OnState(eState, eStatePrevious);
	eStatePrevious:= eState;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Switch" Id="{8661b760-0fa5-007a-151c-7fe805544fbb}" FolderPath="Command\">
      <Declaration><![CDATA[{attribute 'TcRpcEnable':='1'}
METHOD PUBLIC M_Switch : BOOL
VAR_INPUT
	eMode:		E_Mode;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF eMode = E_Mode.Undefined THEN
	M_Switch:= TRUE;
ELSE
	IF THIS^.eMode = eMode THEN
		M_Switch:= TRUE;
	ELSE
		eModeRequest:= eMode;	
	END_IF	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Resetting" Id="{9e5fa12d-dc9f-0271-3ab1-62fbd4e3c4cb}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Resetting : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Resetting
M_Resetting:= True;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Completing" Id="{a6f0ed18-708c-02a1-0990-4f4a1b2b5a3a}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Completing : BOOL // Return state complete
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Completing
M_Completing:= True;]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_State" Id="{b623cabe-ac07-05b3-3398-a07000fa3104}">
      <Declaration><![CDATA[{attribute 'OPC.UA.DA.Property' := '1'}
{attribute 'monitoring' := 'call'}
PROPERTY PUBLIC P_State : E_State]]></Declaration>
      <Get Name="Get" Id="{f9b2e5f6-eb20-00e6-39d5-99b5e96a63c5}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_State:= eState;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_init" Id="{badf4989-5758-03ee-2097-25283f2924c2}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
	bInCopyCode : BOOL:= TRUE;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[M_Initialise();]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Request" Id="{bdd740c0-4464-0c1b-399c-16ea1aed66b8}" FolderPath="Command\">
      <Declaration><![CDATA[{attribute 'TcRpcEnable':='1'}
METHOD PUBLIC M_Request : BOOL
VAR_INPUT
	eRequest:	E_Request;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Request component states / Command Pattern
CASE eRequest OF
	E_Request.Undefined:
		M_Request:= TRUE;
		
	E_Request.Start:
		IF eState = E_State.Execute THEN
			M_Request:= TRUE;
		ELSE
			eStateRequest:= eRequest;				
		END_IF
		
	E_Request.Pause:
		IF eState >= E_State.Paused THEN
			M_Request:= TRUE;
		ELSE
			IF eStateRequest < eRequest THEN
				eStateRequest:= eRequest;				
			END_IF		
		END_IF
		
	E_Request.Hold:
		IF eState >= E_State.Held THEN
			M_Request:= TRUE;
		ELSE
			IF eStateRequest < eRequest THEN
				eStateRequest:= eRequest;				
			END_IF		
		END_IF
		
	E_Request.Complete:
		IF eState = E_State.Completed THEN
			M_Request:= TRUE;
		ELSE
			IF eStateRequest < eRequest THEN
				eStateRequest:= eRequest;				
			END_IF		
		END_IF
		
	E_Request.Stop:
		IF eState = E_State.Stopped THEN
			M_Request:= TRUE;
		ELSE
			IF eStateRequest < eRequest THEN
				eStateRequest:= eRequest;				
			END_IF		
		END_IF
		
	E_Request.Abort:
		IF eState = E_State.Aborted THEN
			M_Request:= TRUE;
		ELSE
			IF eStateRequest < eRequest THEN
				eStateRequest:= eRequest;				
			END_IF		
		END_IF

	E_Request.Reset:
		IF eState = E_State.Idle THEN
			M_Request:= TRUE;
		ELSE
			eStateRequest:= eRequest;			
		END_IF
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Running" Id="{c183b15a-6638-0cac-2cc8-e5cd884bfdf8}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Running
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Will be called in all running states, for monitorings which not only in one state active]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Pausing" Id="{c4aa5598-07e6-01cf-3056-2987866628ee}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Pausing : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Holding
M_Pausing:= True;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Notify" Id="{d8f6f158-ff95-0e77-1e0e-c4742a312f00}">
      <Declaration><![CDATA[METHOD PUBLIC M_Notify
VAR_INPUT
	stEvent:		ST_Event;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Method will be called by the Observer
// Handle  state request
CASE stEvent.eRequest OF
    E_Request.Pause:
		IF fbEvent.P_Settings.bPause THEN
			M_Request(stEvent.eRequest);
		END_IF
		
    E_Request.Hold:
		IF fbEvent.P_Settings.bHold THEN
			M_Request(stEvent.eRequest);
		END_IF
		
	E_Request.Complete:
		IF fbEvent.P_Settings.bComplete THEN
			M_Request(stEvent.eRequest);
		END_IF
		
	E_Request.Stop, E_Request.Abort:
		M_Request(stEvent.eRequest);
END_CASE

// Select new mode
M_Switch(stEvent.eMode);

// Notify all other components in observer
fbEvent.P_Observer.M_Notify(stEvent);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Idle" Id="{ee6be811-c2b8-059d-2e73-08e8c6ee3020}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Idle
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// State Idle]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Aborted" Id="{f47ae709-6b97-0869-103e-462a949d9bd1}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Aborted
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Held" Id="{f81e94c9-4b34-0591-0431-3c8c41c7dc3f}" FolderPath="State\">
      <Declaration><![CDATA[METHOD PROTECTED M_Held
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>